;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ptplayer_test
;;
;; Demonstrates all the features of ptplayer lib
;; E-Penguin, 2023
;;
WBStartup

;; Include important NewTypes and convenience function
;; for converting bb Sound object to sfx structure
XINCLUDE "ptplayer_include.bb2"

; Load a mod into bank 0
Gosub gs_LoadMod

; Load some sounds into Sound 0..3
Gosub gs_LoadSfx

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTInstall
; Install the CIA MOD player routine (1=PAL,0=NTSC)
MTInstall 1

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTInit
; Initialise the player with Bank 0
MTInit 0,0

; Initialise some data structures for the sfx

Gosub gs_InitSfxStructs

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTPlay
; Start the play loop
MTPlay On

; Wait 10s
VWait 500

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTIsEnabled
; Confirm it is playing
If MTIsEnabled = True
  NPrint "Music is playing"
EndIf

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Show the three ways of playing a sound effect
;
; MTPlayFx
For i=0 To 5
*status.SfxChanStatus = MTPlayFx(&sfx0)
  Print "Playing effect on channel "
  NPrint *status\n_index
  VWait 50
Next i

; MTSoundFX (with sound object)
For i=0 To 5
  MTSoundFX 1,64
  NPrint "Playing effect with sound object 1"
  VWait 50
Next i

; MTSoundFX (with sample pointer)
*_snd.sound = Addr Sound(2)  ; Get the pointer to the sound
For i=0 To 5
  MTSoundFX *_snd\_data, *_snd\_length, *_snd\_period, 64
  NPrint "Playing effect with sound object 2"
  VWait 50
Next i

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMasterVolume
; Turn down and back up
NPrint "Fade Out"
Gosub gs_FadeOut : VWait 50
NPrint "Fade In"
Gosub gs_FadeIn  : VWait 50


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMusicMask
; Reserve channels for music
MTMusicMask 5 ; Reserve channel 0 and 3
VWait 50

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMusicChannels
; Get reserved channels for music
music_chans.b = MTMusicChannels
Print "Channels reserved for music: "
NPrint music_chans
VWait 50


MTEnd    ; Stop playing the current mod
MTRemove ; Remove the CIA MOD player routine


End

.gs_LoadMod
; Using public domain mod from
; https://modarchive.org/index.php?request=view_by_moduleid&query=178172
LoadBank 0,"drozerix_-_neon_techno.mod",2
Return

.gs_LoadSfx
; SFX Credits:
; Dinosaur roar : Groadr @ freesound.org
; Pterodactyl : dinodilopho @ freesound.org
; Ho Ho Ho : sovy @ freesound.org
; Ow : Topschool @ freesound.org
LoadSound 0,"ow-sound.8svx"
LoadSound 1,"dragon-roar.8svx"
LoadSound 2,"pterodactyl.8svx"
LoadSound 3,"ho-ho-hooo.8svx"
Return

.gs_InitSfxStructs
; Initialize a sfx pointers for ptplayer
; {*pointer.sfx, sound_index, volume, channel (-1=best), priority (>0) }
DEFTYPE .sfx sfx0
DEFTYPE .sfx sfx1
DEFTYPE .sfx sfx2
DEFTYPE .sfx sfx3

SFXInit{&sfx0,0,64,-1,1}
SFXInit{&sfx1,1,64,-1,1}
SFXInit{&sfx2,2,64,2,1}
SFXInit{&sfx3,3,64,3,1}
Return

.gs_FadeOut
MTMasterVolume 32 : VWait 50
MTMasterVolume 16 : VWait 50
MTMasterVolume 8  : VWait 50
MTMasterVolume 4  : VWait 50
MTMasterVolume 2  : VWait 50
MTMasterVolume 0  : VWait 50
Return

.gs_FadeIn
MTMasterVolume 2  : VWait 50
MTMasterVolume 4  : VWait 50
MTMasterVolume 8  : VWait 50
MTMasterVolume 16 : VWait 50
MTMasterVolume 32 : VWait 50
MTMasterVolume 64 : VWait 50
Return





